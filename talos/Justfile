cluster-name := "tale-lab"
endpoint-ip := "https://172.31.42.100:6443"
cilium-version := "1.18.0"

gen-node-config node: gen-base-config
	@echo "ðŸ‘‰ Rendering controlplane.yaml for {{node}}"
	talos machineconfig patch {{justfile_directory()}}/control.gen.yaml \
		--output {{justfile_directory()}}/{{node}}.gen.yaml \
		--patch @{{justfile_directory()}}/patches/{{node}}.yaml \

gen-base-config: config-sops
	@echo "ðŸ‘‰ Rendering Talos configuration files"
	@just --justfile {{justfile_directory()}}/../k8s/cilium/Justfile gen-cilium-template
	talos gen config {{cluster-name}} {{endpoint-ip}} \
		--with-docs=false --with-examples=false --output-types controlplane \
		--force --output {{justfile_directory()}}/control.gen.yaml \
		--with-secrets {{justfile_directory()}}/secrets.gen.yaml \
		--config-patch @{{justfile_directory()}}/patches/shared.yaml \
		--config-patch @{{justfile_directory()}}/patches/tailscale.gen.yaml \
		--config-patch @{{justfile_directory()}}/patches/cilium.gen.yaml

config-sops:
	@echo "ðŸ‘‰ Decrypting Talos secrets"
	sops -d {{justfile_directory()}}/secrets.sops.yaml > {{justfile_directory()}}/secrets.gen.yaml
	sops -d {{justfile_directory()}}/patches/tailscale.sops.yaml > {{justfile_directory()}}/patches/tailscale.gen.yaml

clean:
	@echo "ðŸ‘‰ Cleaning up generated files"
	rm -f {{justfile_directory()}}/control.gen.yaml
	rm -f {{justfile_directory()}}/node-*.gen.yaml
	rm -f {{justfile_directory()}}/secrets.gen.yaml
	rm -f {{justfile_directory()}}/patches/tailscale.gen.yaml
